plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.0'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.7'

    id 'jacoco'
    id 'org.jlleitschuh.gradle.ktlint' version '13.0.0'
}

group = 'printscript'
version = '0.0.1-SNAPSHOT'
description = 'Runner Service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// Versión de PrintScript que va a usar el runner
def printscriptVersion = '0.2.4-SNAPSHOT'

repositories {
    mavenCentral()

    // GitHub Packages para PrintScript
    maven {
        url = uri("https://maven.pkg.github.com/IngSis25/PrintScript")
        credentials {
            username = project.properties.get("gpr.user") ?: System.getenv("GITHUB_ACTOR") // amgarber
            password = project.properties.get("gpr.key") ?: System.getenv("GITHUB_TOKEN") // Tu token
        }
    }

    // GitHub Packages para redis-streams
    maven {
        url = uri("https://maven.pkg.github.com/austral-ingsis/class-redis-streams")
        credentials {
            username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user")
            password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
        }
    }
}

dependencies {
    // SPRING BOOT BASE
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

// Forzamos que el starter reactive use 3.3.4 y además NO meta spring-boot-reactor

    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'
    implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'
    implementation 'com.google.code.gson:gson:2.10.1'

    // REDIS STREAMS
    implementation 'org.austral.ingsis:redis-streams-flux:0.1.13'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

    implementation "io.printscript:runner:${printscriptVersion}"
    implementation "io.printscript:interpreter:${printscriptVersion}"
    implementation "io.printscript:lexer:${printscriptVersion}"
    implementation "io.printscript:parser:${printscriptVersion}"
    implementation "io.printscript:analyzer:${printscriptVersion}"
    implementation "io.printscript:formatter:${printscriptVersion}"
    implementation "io.printscript:common:${printscriptVersion}"



    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    runtimeOnly 'org.postgresql:postgresql'

    // TESTS
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux' // para WebTestClient
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.1.0"
    testImplementation 'io.mockk:mockk:1.13.8'
    testImplementation 'org.amshove.kluent:kluent:1.73'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

allOpen {
    annotation 'jakarta.persistence.Entity'
    annotation 'jakarta.persistence.MappedSuperclass'
    annotation 'jakarta.persistence.Embeddable'
}

// JUnit 5
tasks.named('test') {
    useJUnitPlatform()
}

// JaCoCo
jacoco {
    toolVersion = '0.8.11'
}

tasks.withType(Test).configureEach {
    finalizedBy(tasks.named('jacocoTestReport'))
}

tasks.named('jacocoTestReport') {
    dependsOn(tasks.named('test'))
    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)
    }
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn(tasks.named('test'))
    violationRules {
        rule {
            limit { minimum = 0.60 }
        }
    }
}

tasks.named('check') {
    dependsOn(tasks.named('jacocoTestCoverageVerification'))
    dependsOn(tasks.named('ktlintCheck'))
}

// KTLINT
tasks.named('check') {
    dependsOn(tasks.named('ktlintCheck'))
}

// Git hooks
tasks.register('installGitHooks', Copy) {
    from("$rootDir/hooks/pre-commit")
    into("$rootDir/.git/hooks")
}
